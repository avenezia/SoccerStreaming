#Reading build mode, currently default is debug, it will be release
buildMode = ARGUMENTS.get('mode', 'debug')
assert(buildMode == "release" or buildMode == "debug")

buildFolder = "build/" + buildMode + "/"
networkFolder = "network/"
websiteFolder = "website/"

compilerFlags = ["-std=c++11", "-Wall", "-Werror"]
if buildMode == "debug":
    compilerFlags.append("-g")
Export('compilerFlags')    
    
env = Environment()
env.Append(CPPFLAGS = compilerFlags)
env.Append(CPPPATH = [networkFolder, websiteFolder, "/usr/local/include/"])
env.Append(LIBS=["boost_regex"])
env.ParseConfig('pkg-config --libs curlpp')

objects = []
#network part
networkObjects = SConscript(networkFolder + "SConscript", variant_dir = buildFolder + networkFolder, duplicate = 0)
objects.extend(networkObjects)

#website part
websiteObjects = SConscript(websiteFolder + "SConscript", variant_dir = buildFolder + websiteFolder, duplicate = 0)
objects.extend(websiteObjects)

mainObject = env.Object(source = "main.cpp")
objects.append(mainObject)
env.Program("main", objects)