#Reading build mode, currently default is debug, it will be release
buildMode = ARGUMENTS.get('mode', 'debug')
assert(buildMode == "release" or buildMode == "debug")

buildFolder = "build/" + buildMode + "/"
networkFolder = "network/"
parserFolder  = "parser/"
websiteFolder = "website/"

compilerFlags = ["-std=c++11", "-Wall", "-Werror"]
if buildMode == "debug":
    compilerFlags.append("-g")
Export('compilerFlags')    
    
env = Environment()
env.Append(CPPFLAGS = compilerFlags)
env.Append(CPPPATH = [networkFolder, parserFolder, websiteFolder, "/usr/local/include/"])
env.Append(LIBS=["boost_regex", "glog"])
env.ParseConfig('pkg-config --libs curlpp')
env.ParseConfig('pkg-config --libs gumbo')

objects = []

#network part
networkObjects = SConscript(networkFolder + "SConscript", variant_dir = buildFolder + networkFolder, duplicate = 0)
objects.extend(networkObjects)

#parser part
parserObjects = SConscript(parserFolder + "SConscript", variant_dir = buildFolder + parserFolder, duplicate = 0)
objects.extend(parserObjects)

#website part
websiteObjects = SConscript(websiteFolder + "SConscript", variant_dir = buildFolder + websiteFolder, duplicate = 0)
objects.extend(websiteObjects)

mainObject = env.Object(source = "main.cpp")
objects.append(mainObject)
env.Program("main", objects)